Package goplusplus;

/*==========================*/
/*			Helpers			*/
/*==========================*/
Helpers
	all 		= [0 .. 0xFF];
	tab   		= 9;
	cr    		= 13;
	lf    		= 10;
	newline		= cr | lf | cr lf;
	all_but_eol	= [all - [cr + lf]];
	dec_digit 	= ['0'..'9'];
	oct_digit	= ['0'..'7'];
	hex_digit	= ['0'..'9'] | ['A'..'F'] | ['a'..'f'];
	lowercase 	= ['a'..'z'];
	uppercase 	= ['A'..'Z'];
	letter  	= lowercase | uppercase;
	idletter 	= letter | '_';
	idchar  	= letter | '_' | dec_digit;
	printable 	= [0x20 .. 0xFF];
	char 		= printable | tab;
	comment_lin = '//';
	comment_beg = '/*';
	comment_end = '*/';
	dec 		= ['1'..'9'] (dec_digit)*;
	oct			= '0' (oct_digit)*;
	hex			= '0' ('x' | 'X') (hex_digit)+;


/*==========================*/
/*			Tokens			*/
/*==========================*/
Tokens
	eol			= newline;
	blank 		= ' ' | tab;

/*Keywords*/
	break		= 'break';
	default		= 'default';
	func		= 'func';
	interface	= 'interface';
	select		= 'select';
	case		= 'case';
	defer		= 'defer';
	go 			= 'go';
	map			= 'map';
	struct		= 'struct';
	chan		= 'chan';
	else		= 'else';
	goto		= 'goto';
	package		= 'package';
	switch		= 'switch';
	const		= 'const';
	fallthrough	= 'fallthrough';
	if			= 'if';
	range		= 'range';
	type		= 'type';
	continue	= 'continue';
	for			= 'for';
	import		= 'import';
	return		= 'return';
	var			= 'var';
	basic_types	= 'int' | 'float64' | 'bool' | 'rune' | 'string';
	print		= 'print';
	println		= 'println';
	append		= 'append';

/*Operators*/
	add			= '+';
	sub			= '-';
	mul			= '*';
	div			= '/';
	mod			= '%';
	bit_and		= '&';
	bit_or		= '|';
	caret		= '^';
	lshift		= '<<';
	rshift		= '>>';
	bitclear	= '&^';
	pluseq		= '+=';
	subeq		= '-=';
	muleq		= '*=';
	diveq		= '/=';
	modeq		= '%=';
	andeq		= '&=';
	oreq		= '|=';
	xoreq		= '^=';
	lshifteq	= '<<=';
	rshifteq	= '>>=';
	bitcleareq	= '&^=';
	and			= '&&';
	or			= '||';
	receive		= '<-';
	increment	= '++';
	decrement	= '--';
	eq			= '==';
	lt			= '<';
	gt			= '>';
	assign		= '=';
	not			= '!';
	noteq		= '!=';
	leq			= '<=';
	geq			= '>=';
	shortdecl	= ':=';
	varargs		= '...';
	leftpar		= '(';
	leftsq		= '[';
	leftcurl	= '{';
	comma		= ',';
	dot 		= '.';
	rightpar	= ')';
	rightsq		= ']';
	rightcurl	= '}';
	semi		= ';';
	colon		= ':';
	
/*Comment*/
	line_comment 		= comment_lin all_but_eol*;
	block_comment	 	= comment_beg all* comment_end;

/*Literals*/
	escaped_char 		= '\' ('a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '\' | ''' | '"');
	int_lit				= dec | oct | hex;
	float_lit 			= ((dec_digit)* '.' (dec_digit)+) | ((dec_digit)+ '.' (dec_digit)*);
	rune_lit			= ;///////////////////////////////// need a fix
	string_lit			= ;///////////////////////////////// need a fix
	raw_string 			= ''' (char | newline)* ''';
	interpreted_string 	= '"' (char | newline)* '"';

/*Identifiers*/
	id = idletter idchar*;


Ignored Tokens
	blank, eol, line_comment, block_comment;


/*==========================*/
/*		Productions			*/
/*==========================*/
Productions
	prod_program {-> ast_program} =
		package id semi prod_decl*
			{-> New ast_program(id, [prod_decl.ast_decl])}
	;

	prod_decl {-> ast_decl} =
		{variable} var prod_var_decl semi
			{-> New ast_decl.var_dec([prod_var_decl.ast_var_decl])}
	|	{type} type prod_type_decl semi
			{-> New ast_decl.type_dec([prod_type_decl.ast_type_decl])}
	|	{function} func prod_func_decl semi
			{-> New ast_decl.func_dec(prod_func_decl.ast_func_decl)}
	;

	prod_id_list {-> id+} =
		 id prod_id_tail*
		 	{-> [id, prod_id_tail.id]}
	;

	prod_id_tail {-> id} =
		comma id
			{-> id}
	;

	prod_exp_list {-> ast_exp*} =
		prod_exp prod_exp_tail*
			{-> [prod_exp.ast_exp, prod_exp_tail.ast_exp]}
	;

	prod_exp_tail {-> ast_exp} =
		comma prod_exp
			{-> prod_exp.ast_exp}
	;

	/*======================*/
	/*		Declaration		*/
	/*======================*/

	prod_var_decl {-> ast_var_decl*} = 
		{single}	prod_var_decl_single
			{-> [prod_var_decl_single.ast_var_decl]}
	|	{list}		leftpar prod_var_decl_single_semi* rightpar
			{-> [prod_var_decl_single_semi.ast_var_decl]}
	;

	prod_var_decl_single {-> ast_var_decl} = 
		{type}		prod_id_list prod_type_exp
			{-> New ast_var_decl.type([prod_id_list.id], prod_type_exp.ast_type_exp)}
	|	{expr}		prod_id_list assign prod_exp_list
			{-> New ast_var_decl.exp([prod_id_list.id], [prod_exp_list.ast_exp])}
	|	{type_exp}	prod_id_list prod_type_exp assign prod_exp_list
			{-> New ast_var_decl.type_exp([prod_id_list.id], prod_type_exp.ast_type_exp, [prod_exp_list.ast_exp])}
	;

	prod_var_decl_single_semi {-> ast_var_decl} =
		prod_var_decl_single semi
			{-> prod_var_decl_single.ast_var_decl}
	;

	prod_type_decl {-> ast_type_decl*} =
		{single}	prod_type_decl_single
			{-> [prod_type_decl_single.ast_type_decl]}
	|	{list}		leftpar prod_type_decl_single_semi* rightpar
			{-> [prod_type_decl_single_semi.ast_type_decl]}
	;

	prod_type_decl_single {-> ast_type_decl} =
		prod_id_list prod_type_exp
			{-> New ast_type_decl([prod_id_list.id], prod_type_exp.ast_type_exp)}
	;

	prod_type_decl_single_semi {-> ast_type_decl} =
		prod_type_decl_single semi
			{-> prod_type_decl_single.ast_type_decl}
	;

	prod_type_exp {-> ast_type_exp} =
		{basic}		basic_types
			{-> New ast_type_exp.basic(basic_types)}
	|	{slice}		leftsq rightsq prod_type_exp
			{-> New ast_type_exp.slice(prod_type_exp.ast_type_exp)}
	|	{array}		leftsq int_lit rightsq prod_type_exp
			{-> New ast_type_exp.array(int_lit, prod_type_exp.ast_type_exp)}
	|	{struct}	struct leftcurl prod_struct_field* rightcurl
			{-> New ast_type_exp.struct([prod_struct_field.ast_struct_field])}
	;

	prod_struct_field {-> ast_struct_field} =
		prod_id_list prod_type_exp semi
			{-> New ast_struct_field([prod_id_list.id], prod_type_exp.ast_type_exp)}
	;

	prod_func_decl {-> ast_func_decl} =
		id leftpar prod_func_param? rightpar prod_type_exp? prod_block
			{-> New ast_func_decl(id, [prod_func_param.ast_func_param], prod_type_exp.ast_type_exp, [prod_block.ast_stm])}
	;

	prod_func_param {-> ast_func_param*} = 
		prod_id_list prod_type_exp prod_func_param_tail*
			{-> [New ast_func_param([prod_id_list.id], prod_type_exp.ast_type_exp), prod_func_param_tail.ast_func_param]}
	;

	prod_func_param_tail {-> ast_func_param}=
		comma prod_id_list prod_type_exp
			{-> New ast_func_param([prod_id_list.id], prod_type_exp.ast_type_exp)}
	;

	/*======================*/
	/*		Statement		*/
	/*======================*/

	prod_block {-> ast_stm*} =
		leftcurl prod_stm* rightcurl
			{-> [prod_stm.ast_stm]}
	;
	
	prod_stm {-> ast_stm} =
		{simple}		prod_simple_stm_semi
			{-> prod_simple_stm_semi.ast_stm}
	|	{block}			prod_block semi
			{-> New ast_stm.block([prod_block.ast_stm])}
	|	{variable_decl}	var prod_var_decl semi
			{-> New ast_stm.var_decl([prod_var_decl.ast_var_decl])}
	|	{type_decl}		type prod_type_decl semi
			{-> New ast_stm.type_decl([prod_type_decl.ast_type_decl])}
	|	{return}		return prod_exp? semi
			{-> New ast_stm.return(prod_exp.ast_exp)}
	|	{break}			break semi
			{-> New ast_stm.break()}
	|	{continue}		continue semi 
			{-> New ast_stm.continue()}
	|	{print}			print leftpar prod_exp_list rightpar semi
			{-> New ast_stm.print([prod_exp_list.ast_exp])}
	|	{println}		println leftpar prod_exp_list rightpar semi
			{-> New ast_stm.println([prod_exp_list.ast_exp])}
	|	{if}			if_stm
			{-> if_stm.ast_stm}
	|	{switch}		switch prod_simple_stm_semi? prod_exp leftcurl prod_switch_stm* rightcurl
			{->  New ast_stm.switch(prod_simple_stm_semi.ast_stm,prod_exp.ast_exp,[prod_switch_stm.ast_switch_stm])}
	|	{for}			prod_for_stm
			{-> prod_for_stm.ast_stm} 
	;
	
	if_stm {-> ast_stm} =
		{if}			if prod_simple_stm_semi? prod_exp prod_block
		{-> New ast_stm.shortif(prod_simple_stm_semi.ast_stm, prod_exp.ast_exp, [prod_block.ast_stm])}
	|	{ifelse}		if prod_simple_stm_semi? prod_exp [if_block]:prod_block else [else_block]:prod_block
		{-> New ast_stm.longif(prod_simple_stm_semi.ast_stm, prod_exp.ast_exp,[if_block.ast_stm],[else_block.ast_stm])}
	|	{if_else_if}	if prod_simple_stm_semi? prod_exp prod_block else if_stm
		{-> New ast_stm.longif(prod_simple_stm_semi.ast_stm, prod_exp.ast_exp,[prod_block.ast_stm],[if_stm.ast_stm])}
	;
	
	prod_switch_stm {-> ast_switch_stm} = 
		prod_switch_case prod_stm* prod_fallthrough_stm? 
		{-> New ast_switch_stm(prod_switch_case.ast_switch_case,[prod_stm.ast_stm],prod_fallthrough_stm.ast_fallthrough_stm)}
	;
	
	prod_switch_case {-> ast_switch_case} = 
		{exp}		case prod_exp_list colon
			{-> New ast_switch_case.case([prod_exp_list.ast_exp])}
	|	{default}	default colon
			{-> New ast_switch_case.default()}
	;
	
	prod_fallthrough_stm {-> ast_fallthrough_stm}= 
		fallthrough semi {-> New ast_fallthrough_stm()};
		
	prod_for_stm {-> ast_stm} =
		{while}		for prod_exp? prod_block
			{-> New ast_stm.for(Null,prod_exp.ast_exp,Null,[prod_block.ast_stm])}
	|	{long}		for prod_simple_stm_semi? prod_exp? semi prod_simple_stm prod_block
			{-> New ast_stm.for(prod_simple_stm_semi.ast_stm,prod_exp.ast_exp,prod_simple_stm.ast_stm,[prod_block.ast_stm])}
	;
	
	prod_simple_stm_semi {-> ast_stm} =
		prod_simple_stm semi
		{-> prod_simple_stm.ast_stm}
	;
	
	prod_simple_stm {-> ast_stm} =
		{empty}
		{-> New ast_stm.empty()}
	|	{expression}	prod_exp
		{-> New ast_stm.exp(prod_exp.ast_exp)}
	|	{increment}		prod_exp increment
		{-> New ast_stm.inc_dec(prod_exp.ast_exp, New post_op.inc())}
	|	{decrement}		prod_exp decrement
		{-> New ast_stm.inc_dec(prod_exp.ast_exp, New post_op.dec())}
	|	{assign}		[left]:prod_exp_list assign [right]:prod_exp_list
		{-> New ast_stm.assign([left.ast_exp],[right.ast_exp])}
	|	{short_var_decl}[ids]:prod_exp_list shortdecl [exps]:prod_exp_list
		{-> New ast_stm.short_decl([ids.ast_exp],[exps.ast_exp])}
	|	{op_assign}		id prod_op_assign prod_exp
		{-> New ast_stm.op_assign(id,prod_op_assign.ast_op_assign,prod_exp.ast_exp)}
	;
	
	prod_op_assign {-> ast_op_assign} =
		{add_eq}	pluseq {-> New ast_op_assign.add_eq()}
	|	{sub_eq}	subeq	{-> New ast_op_assign.sub_eq()}
	|	{mul_eq}	muleq	{-> New ast_op_assign.mul_eq()}
	|	{div_eq}	diveq	{-> New ast_op_assign.div_eq()}
	|	{mod_eq}	modeq	{-> New ast_op_assign.mod_eq()}
	|	{bitor_eq}	oreq	{-> New ast_op_assign.bitor_eq()}
	|	{bitand_eq}	andeq	{-> New ast_op_assign.bitand_eq()}
	|	{caret_eq}		xoreq	{-> New ast_op_assign.caret_eq()}
	|	{lshift_eq}	lshifteq	{-> New ast_op_assign.lshift_eq()}
	|	{rshift_eq}	rshifteq	{-> New ast_op_assign.rshift_eq()}
	|	{bitclear_eq}	bitcleareq	{-> New ast_op_assign.bitclear_eq()}
	;

	/*======================*/
	/*		Expression		*/
	/*======================*/

	prod_exp {-> ast_exp} =
		{prec2} prod_bin_prec2
			{-> prod_bin_prec2.ast_exp}
	|	{or} prod_exp or prod_bin_prec2
			{-> New ast_exp.binary_op(prod_exp.ast_exp, New ast_binary_op.or(), prod_bin_prec2.ast_exp)}
	;

	prod_bin_prec2 {-> ast_exp} =
		{prec3} prod_bin_prec3
			{-> prod_bin_prec3.ast_exp}
	|	{and} prod_bin_prec2 and prod_bin_prec3
			{-> New ast_exp.binary_op(prod_bin_prec2.ast_exp, New ast_binary_op.and(), prod_bin_prec3.ast_exp)}
	;

	prod_bin_prec3 {-> ast_exp} =
		{prec4} prod_bin_prec4
			{-> prod_bin_prec4.ast_exp}
	|	{eq} prod_bin_prec3 eq prod_bin_prec4
			{-> New ast_exp.binary_op(prod_bin_prec3.ast_exp, New ast_binary_op.eq(), prod_bin_prec4.ast_exp)}
	|	{noteq} prod_bin_prec3 noteq prod_bin_prec4
			{-> New ast_exp.binary_op(prod_bin_prec3.ast_exp, New ast_binary_op.noteq(), prod_bin_prec4.ast_exp)}
	|	{lt} prod_bin_prec3 lt prod_bin_prec4
			{-> New ast_exp.binary_op(prod_bin_prec3.ast_exp, New ast_binary_op.lt(), prod_bin_prec4.ast_exp)}
	|	{leq} prod_bin_prec3 leq prod_bin_prec4
			{-> New ast_exp.binary_op(prod_bin_prec3.ast_exp, New ast_binary_op.leq(), prod_bin_prec4.ast_exp)}
	|	{gt} prod_bin_prec3 gt prod_bin_prec4
			{-> New ast_exp.binary_op(prod_bin_prec3.ast_exp, New ast_binary_op.gt(), prod_bin_prec4.ast_exp)}
	|	{geq} prod_bin_prec3 geq prod_bin_prec4
			{-> New ast_exp.binary_op(prod_bin_prec3.ast_exp, New ast_binary_op.geq(), prod_bin_prec4.ast_exp)}
	;

	prod_bin_prec4 {-> ast_exp} =
		{prec5} prod_bin_prec5
			{-> prod_bin_prec5.ast_exp}
	|	{plus} prod_bin_prec4 add prod_bin_prec5
			{-> New ast_exp.binary_op(prod_bin_prec4.ast_exp, New ast_binary_op.add(), prod_bin_prec5.ast_exp)}
	|	{minus} prod_bin_prec4 sub prod_bin_prec5
			{-> New ast_exp.binary_op(prod_bin_prec4.ast_exp, New ast_binary_op.sub(), prod_bin_prec5.ast_exp)}
	|	{bitor} prod_bin_prec4 bit_or prod_bin_prec5
			{-> New ast_exp.binary_op(prod_bin_prec4.ast_exp, New ast_binary_op.bitor(), prod_bin_prec5.ast_exp)}
	|	{caret} prod_bin_prec4 caret prod_bin_prec5
			{-> New ast_exp.binary_op(prod_bin_prec4.ast_exp, New ast_binary_op.caret(), prod_bin_prec5.ast_exp)}
	;
	
	prod_bin_prec5 {-> ast_exp} =
		{unary} prod_unary
			{-> prod_unary.ast_exp}
	|	{mult} prod_bin_prec5 mul prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.mul(), prod_unary.ast_exp)}
	|	{div} prod_bin_prec5 div prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.div(), prod_unary.ast_exp)}
	|	{mod} prod_bin_prec5 mod prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.mod(), prod_unary.ast_exp)}
	|	{lshift} prod_bin_prec5 lshift prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.lshift(), prod_unary.ast_exp)}
	|	{rshift} prod_bin_prec5 rshift prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.rshift(), prod_unary.ast_exp)}
	|	{bitand} prod_bin_prec5 bit_and prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.bitand(), prod_unary.ast_exp)}
	|	{bitclear} prod_bin_prec5 bitclear prod_unary
			{-> New ast_exp.binary_op(prod_bin_prec5.ast_exp, New ast_binary_op.bitclear(), prod_unary.ast_exp)}
	;
	
	prod_unary {-> ast_exp} =
		{pos} add prod_unary
			{-> New ast_exp.unary_op(New ast_unary_op.plus(), prod_unary.ast_exp)}
	|	{neg} sub prod_unary
			{-> New ast_exp.unary_op(New ast_unary_op.minus(), prod_unary.ast_exp)}
	|	{not} not prod_unary
			{-> New ast_exp.unary_op(New ast_unary_op.not(), prod_unary.ast_exp)}
	|	{caret} caret prod_unary
			{-> New ast_exp.unary_op(New ast_unary_op.caret(), prod_unary.ast_exp)}
	|	{sin}	prod_single
			{-> prod_single.ast_exp}
	;
	
	prod_single {-> ast_exp} =
		{paren} leftpar prod_exp rightpar
			{-> New ast_exp.paren(prod_exp.ast_exp)}
	|	{prod_id} id
			{-> New ast_exp.id(id)}
	|	{prod_lit} prod_literals
			{-> New ast_exp.lit(prod_literals.ast_literal)}
	;

	prod_literals {-> ast_literal} =
		{prod_int} int_lit
			{-> New ast_literal.int(int_lit)}
	|	{prod_float} float_lit
			{-> New ast_literal.float(float_lit)}
	|	{prod_rune} rune_lit
			{-> New ast_literal.rune(rune_lit)}
	|	{prod_string} string_lit
			{-> New ast_literal.string(string_lit)}
	;


/*==========================*/
/*			AST				*/
/*==========================*/
Abstract Syntax Tree
	ast_program = 
		[package]:id [decl]:ast_decl*
	;
	
	ast_decl = 
		{var_dec}			ast_var_decl+
	|	{type_dec} 			ast_type_decl+
	|	{func_dec}			ast_func_decl
	;
	
	ast_var_decl = 
		{type} 	id+ ast_type_exp
	|	{exp} 	id+ ast_exp+
	|	{type_exp} 	id+ ast_type_exp ast_exp+
	;

	ast_type_decl = 
		id+ ast_type_exp
	;
	
	ast_func_decl = 
		id ast_func_param* ast_type_exp? ast_stm*
	;
	
	ast_func_param = 
		id+ ast_type_exp
	;
	
	ast_type_exp = 
		{basic}		basic_types
	|	{slice}		ast_type_exp
	|	{array}		[size]:int_lit ast_type_exp
	|	{struct}	ast_struct_field*
	|	{alias}		id
	;

	ast_struct_field = 
		id+ ast_type_exp
	;
	
	ast_stm =
		{empty}
	|	{exp}			ast_exp
	|	{assign}		[lval]:ast_exp+ [rval]:ast_exp+
	|	{op_assign}		[l]:id ast_op_assign [r]:ast_exp
	|	{var_decl}		ast_var_decl+
	|	{short_decl}	[ids]:ast_exp+ ast_exp+
	|	{type_decl}		ast_type_decl+
	|	{inc_dec}		ast_exp post_op
	|	{print}			ast_exp*
	|	{println}		ast_exp*
	|	{return}		ast_exp?
	|	{shortif}		[init]:ast_stm? [condition]:ast_exp ast_stm*
	|	{longif}		[init]:ast_stm? [condition]:ast_exp [if_stms]:ast_stm* [else_stms]:ast_stm*
	|	{switch}		ast_stm? ast_exp? ast_switch_stm+
	|	{for}			[init]:ast_stm? [condition]:ast_exp? [post]:ast_stm? [body]:ast_stm*
	|	{block}			ast_stm*
	|	{break}
	|	{continue}
	;
	
	ast_exp = 
		{paren}			ast_exp
	|	{id}			id
	|	{lit}			ast_literal
	|	{unary_op}		ast_unary_op ast_exp
	|	{binary_op}		[left]:ast_exp ast_binary_op [right]:ast_exp
	|	{func_call}		[name]:ast_exp [args]:ast_exp*
	|	{append}		id ast_exp
	|	{cast}			ast_type_exp ast_exp
	|	{array_access}	[array]:ast_exp [index]:ast_exp
	|	{field_access}	[struct]:ast_exp [field]:id
	;

	ast_switch_stm = ast_switch_case ast_stm* ast_fallthrough_stm? ;
	
	ast_switch_case = 
		{default}
	|	{case} 		ast_exp+
	;
	
	ast_literal = 
		{int}	 	int_lit
	|	{float} 	float_lit
	|	{rune}		rune_lit
	|	{string}	string_lit
	;

	ast_binary_op = 
		{add}
	|	{sub}
	|	{mul}
	|	{div}
	|	{mod}
	|	{bitor}
	|	{bitand}
	|	{bitxor}
	|	{eq}
	|	{noteq}
	|	{lt}
	|	{leq}
	|	{gt}
	|	{geq}
	|	{caret}
	|	{lshift}
	|	{rshift}
	|	{bitclear}
	|	{or}
	|	{and}
	;
	
	ast_op_assign =
		{add_eq}
	|	{sub_eq}
	|	{mul_eq}
	|	{div_eq}
	|	{mod_eq}
	|	{bitor_eq}
	|	{bitand_eq}
	|	{caret_eq}
	|	{lshift_eq}
	|	{rshift_eq}
	|	{bitclear_eq}
	;
	
	ast_unary_op = 
		{plus}	 
	|	{minus} 
	| 	{not}	
	|	{caret}	
	;
	
	post_op = 
		{inc}
	|	{dec}
	;
	
	ast_fallthrough_stm = ;